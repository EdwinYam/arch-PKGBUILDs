# Maintainer: Leo Mao
pkgname=fasttext-git
_pkgname=fastText
pkgver=0.2.0.r5.g0a57594
pkgrel=1
pkgdesc="Library for fast text representation and classification"
arch=('any')
url="https://github.com/facebookresearch/fastText"
_github='facebookresearch/fastText'
license=('MIT')
depends=('python' 'python-numpy' 'python-scipy')
makedepends=('cmake' 'python-setuptools' 'pybind11')
provides=('fasttext')
conflicts=('fasttext')
source=("git+https://github.com/facebookresearch/fastText")
sha256sums=('SKIP')

pkgver () {
  cd "${_pkgname}"
  (
    set -o pipefail
    git describe --tag 2>/dev/null | sed -e 's/\([^-]*-g\)/r\1/;s/-/./g' -e 's/^v//g'
  )
}

build() {
  cd "${srcdir}/${_pkgname}"
  msg "Building binary"
  mkdir -p build && cd build
  cmake -DCMAKE_INSTALL_PREFIX="/usr" ..
  make

  msg "Building Python binding"
  cd "${srcdir}/${_pkgname}"
  python setup.py build
}

package() {
  cd "${srcdir}/${_pkgname}/build"
  make DESTDIR="$pkgdir" install

  cd "${srcdir}/${_pkgname}"
  # install python binding
  python setup.py install --root="${pkgdir}"/ --optimize=1 --skip-build
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
